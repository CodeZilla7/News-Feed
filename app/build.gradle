/*
 *  Copyright (C) 2019 Eton Otieno
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'

def getProperties(String fileName) {
    final Properties properties = new Properties()
    def file = file(fileName)
    if (file.exists()) {
        file.withInputStream { stream -> properties.load(stream) }
    }
    return properties
}

static def getFromProperties(Properties properties, String name) {
    return properties.getProperty(name) ?: "$name missing"
}

def SECRETS_FILE = getProperties(rootProject.file("secrets.properties").path)
def GUARDIAN_API_KEY = getFromProperties(SECRETS_FILE, "GUARDIAN_API_KEY")
def NEWS_API_KEY = getFromProperties(SECRETS_FILE, "NEWS_API_KEY")

android {
    compileSdkVersion 28
    buildToolsVersion = "28.0.3"
    defaultConfig {
        applicationId = "io.devbits.newsfeed"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode = 1
        versionName = "1.0"
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
        vectorDrawables.useSupportLibrary = true
        buildConfigField("String", "GUARDIAN_API_KEY", GUARDIAN_API_KEY)
        buildConfigField("String", "NEWS_API_KEY", NEWS_API_KEY)

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.incremental": "true"]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility = JavaVersion.VERSION_1_8
        sourceCompatibility = JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.50"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2"
    implementation 'com.google.dagger:dagger:2.24'
    kapt 'com.google.dagger:dagger-compiler:2.24'
    implementation "androidx.activity:activity-ktx:1.1.0-beta01"
    implementation "androidx.fragment:fragment-ktx:1.2.0-beta02"
    implementation "com.google.android.material:material:1.1.0-beta01"
    implementation "androidx.recyclerview:recyclerview:1.1.0-beta05"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0-beta01"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0-beta01"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.2.0-beta01"
    implementation "androidx.room:room-runtime:2.2.0"
    implementation "androidx.room:room-ktx:2.2.0"
    kapt "androidx.room:room-compiler:2.2.0"
    implementation "androidx.navigation:navigation-fragment-ktx:2.2.0-beta01"
    implementation "androidx.navigation:navigation-ui-ktx:2.2.0-beta01"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.viewpager2:viewpager2:1.0.0-beta05"
    implementation "com.github.bumptech.glide:glide:4.10.0"
    kapt "com.github.bumptech.glide:compiler:4.10.0"
    implementation "io.coil-kt:coil:0.7.0"
    implementation "com.squareup.retrofit2:retrofit:2.6.2"
    implementation "com.squareup.retrofit2:converter-gson:2.6.2"
    implementation 'net.danlew:android.joda:2.10.2'
    implementation "com.android.support:multidex:1.0.3"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-2'
}